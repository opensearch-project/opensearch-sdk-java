/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.3.0" apply false
    id 'jacoco'
}


ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}


apply plugin: 'application'
apply from: 'gradle/formatting.gradle'
apply plugin: 'maven-publish'

mainClassName = 'org.opensearch.sdk.sample.helloworld.HelloWorldExtension'

group 'org.opensearch.sdk'
version '1.0.0-SNAPSHOT'


publishing {
    publications {
        group = "org.opensearch"
        version = "1.0.0-SNAPSHOT"
        mavenJava(MavenPublication) {
            from components.java
        }
        sourceCompatibility = 11
        targetCompatibility = 11
    }
}

repositories {
    mavenLocal()
    // Remove the commented code below once TransportService is published to maven
    //maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://d1nvenhzbhpy0q.cloudfront.net/snapshots/lucene/"}
    mavenCentral()
}

configurations {
  requireJavadoc
}

dependencies {
    implementation "org.opensearch:opensearch:3.0.0-SNAPSHOT"
    implementation "org.opensearch.plugin:transport-netty4-client:3.0.0-SNAPSHOT"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
    implementation 'org.opensearch.client:opensearch-rest-client:2.0.0'
    implementation 'org.opensearch.client:opensearch-java:2.0.0'
    implementation "io.netty:netty-all:4.1.73.Final"
    testCompileOnly ("junit:junit:4.13.2") {
        exclude module : 'hamcrest'
        exclude module : 'hamcrest-core'
    }
    implementation 'javax.xml.bind:jaxb-api:2.2.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind: 2.12.6.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml: 2.12.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation "org.opensearch.test:framework:3.0.0-SNAPSHOT"
    requireJavadoc "org.plumelib:require-javadoc:1.0.4"
}

// this task tests for the presence of javadocs but not the content/tags
task requireJavadoc(type: JavaExec) {
  group = 'Documentation'
  description = 'Ensures that Javadoc documentation exists.'
  mainClass = "org.plumelib.javadoc.RequireJavadoc"
  classpath = configurations.requireJavadoc
  args "src/main/java" 
  // javadocs on private methods optional
  args "--dont-require-private=true"
  // javadocs on trivial getters/setters optional
  args "--dont-require-trivial-properties"
}
check.dependsOn requireJavadoc

// this task checks the content/tags of existing javadocs
task javadocStrict(type: Javadoc) {
  group = 'Documentation'
  description = 'Run Javadoc in strict mode: with -Xdoclint:all, on all members.'
  source = sourceSets.main.allJava
  classpath = sourceSets.main.runtimeClasspath
  options.addStringOption('Xdoclint:all', '-quiet')
  options.memberLevel = JavadocMemberLevel.PRIVATE
}
check.dependsOn javadocStrict

// this task runs the helloworld sample extension
task helloWorld(type: JavaExec) {
  group = 'Execution'
  description = 'Run HelloWorld Extension.'
  mainClass = 'org.opensearch.sdk.sample.helloworld.HelloWorldExtension'
  classpath = sourceSets.main.runtimeClasspath
}

// this task runs the helloworld sample extension
task sampleCrud(type: JavaExec) {
    group = 'Execution'
    description = 'Run Sample CRUD Extension.'
    mainClass = 'org.opensearch.sdk.sample.crud.CrudExtension'
    classpath = sourceSets.main.runtimeClasspath
}

test {
    useJUnitPlatform()
    jvmArgs '--enable-preview'
    systemProperty 'tests.security.manager', 'false'
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    jacoco {
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}  
